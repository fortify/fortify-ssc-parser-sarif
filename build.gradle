group 'com.thirdparty'

apply plugin: 'java'
apply plugin: 'idea'

ext {
    versionRegex = '(\\d+)(\\.\\d+)*'
    jacksonVersion = "2.8.4"
}

// plugin version, note the version must match versionRegex.
version = '1.0'

if (!project.hasProperty('release')) {
    // Gradle has been run without option -Prelease - auto increment plugin version
    def time = System.currentTimeMillis().intdiv(1000)
    def hours = time.intdiv(3600)
    version = String.format('%s.%d', version, hours)
}

repositories {
    // repository with fortify artifacts
    maven {
        url  "http://dl.bintray.com/fortify/maven-public"
    }
    mavenCentral()
}

configurations {
    compileExport
    compile.extendsFrom (compileExport)
}

test {
    useJUnitPlatform()
}

dependencies {
    // dependencies provided by plugin runtime
    compile 'com.fortify.plugin:plugin-api:1.0.1'
    compile 'org.slf4j:slf4j-api:1.7.21'

    // plugin specific dependencies
    compileExport(group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: "$jacksonVersion") { transitive = false }
    compileExport(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jacksonVersion") { transitive = false }
    compileExport(group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "$jacksonVersion") { transitive = false }
    compileExport(group: 'org.apache.commons', name: 'commons-lang3', version: "3.9") { transitive = false }
    compileExport(group: 'commons-codec', name: 'commons-codec', version: '1.13') { transitive = false }
    compileExport(group: 'commons-io', name: 'commons-io', version: '2.6') { transitive = false }
    compileExport(group: 'org.mapdb', name: 'mapdb', version: '3.0.7') { transitive = true }
    
    //testImplementation 'org.junit.platform:junit-jupiter-api:5.3.1'
    
    testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1')
    testImplementation(group: 'org.junit.platform', name: 'junit-platform-launcher', version: "1.1.0")
    testRuntimeOnly(group: 'org.junit.jupiter', name:'junit-jupiter-engine', version: '5.3.1')
    testRuntimeOnly(group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1')
}

jar {
    doFirst {
        // check if version matches requirements
        if (!version.matches(versionRegex)) {
            throw new InvalidUserDataException("Plugin version '$version' does not match '$versionRegex'")
        }
    }
    // replace version placeholders in plugin.xml
    filesMatching('plugin.xml') {
        filter {
            it.replaceAll('<!--VERSION-->.*?<!--/VERSION-->', version)
        }
    }
    // include files from compileExport dependencies
    from { configurations.compileExport.collect { it.isDirectory() ? it : zipTree(it).matching { exclude 'META-INF/*' } } }
}

